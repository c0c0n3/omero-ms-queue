import java.nio.file.Files
import java.nio.file.Paths

apply plugin: 'java'
sourceCompatibility = 1.8
targetCompatibility = 1.8

apply plugin: 'jacoco'
apply plugin: 'gradle-execfork-plugin'

dependencies {
    compile project(':kew')

    compile("org.apache.activemq:artemis-server:1.5.5")
    compile("org.apache.activemq:artemis-core-client:1.5.5")

    testCompile libset.test_base
}

jar {
    baseName = 'ome-kew-artemis'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

task clusterMemberDir {
    ext.pwd = Files.createTempDirectory('kew-artemis-tests').toString()
    ext.outLog = Paths.get(pwd, 'artemis-stdout').toString()
    ext.errLog = Paths.get(pwd, 'artemis-stderr').toString()

    doLast {
        printf('>>> Test Cluster Member Working Dir: %s%n', pwd)
    }
    /* printing the path to the tmp dir just in case we need to inspect
     * the logs after a test failure...
     */
}

task startTestClusterMember(
        type: com.github.psxpaul.task.JavaExecFork,
        dependsOn: [testClasses, clusterMemberDir]) {
    stopAfter = test

    main = 'end2end.artemis.ClusterMember'
    classpath = sourceSets.test.runtimeClasspath
    args = ['61617', '61616', 'clustpass', clusterMemberDir.pwd]

    workingDir = clusterMemberDir.pwd
    standardOutput = clusterMemberDir.outLog
    errorOutput = clusterMemberDir.errLog
}

test.dependsOn startTestClusterMember

/* TODO temp workaround to Travis issues.
 * Currently we get this test failure on Travis:
 *   end2end.artemis.ClusterLoadBalancingTest > verifyRoundRobinMessageDelivery FAILED
 *     java.lang.IllegalStateException at ClusterLoadBalancingTest.java:38
 * The test passes on our local build machine, so we need to find out
 * what makes it fail on Travis...
 */
test {
    if (System.properties['test.profile'] == 'travis') {
        exclude '**/end2end/artemis/*'
    }
}
