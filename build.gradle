buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.4.RELEASE")
    }
}

plugins {
    id 'com.github.psxpaul.execfork' version '0.1.5'
    // id 'org.springframework.boot' version '1.5.4.RELEASE'
    /* Ideally we'd use this line instead of the buildscript block above.
     * However if I get rid of it, then the build fails with
     *
     *     Execution failed for task ':bootRepackage'.
     *     > Unable to find main class
     *
     * Even if I keep the buildscript block with the repositories and just
     * delete the dependencies, I still get the same error!
     * Also note that version 2 of the plugin replaced bootRepackage with
     * bootJar and bootWar. So it's probably best to tackle this issue when
     * upgrading to version 2.
     */
}

allprojects {
    group = 'ome'
    version = '1.1.0'

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

task wrapper(type: Wrapper) {
    description 'Sets up the Gradle Wrapper.'
    gradleVersion = '3.4'
}

// library sets shared across sub-projects.
ext.libset = [
    /* Base test deps with compatible version numbers.
     * The jars below are a subset of those in the 'spring-boot-starter-test'
     * which we use in 'server.gradle'. All the other sub-projects don't depend
     * on Spring so we've collected here the common test deps to keep the
     * testing libs the same across the board.
     * NOTES
     * 1. Versions. Same as those specified by 'spring-boot-starter-test'
     * which we use in the server project.
     * 2. Upgrading. Version numbers must be the same as those used by Booty.
     */
    test_base: [
        'org.hamcrest:hamcrest-core:1.3',
        'org.hamcrest:hamcrest-library:1.3',
        'org.mockito:mockito-core:1.10.19',
        'junit:junit:4.12'
    ]
]
